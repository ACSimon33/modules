##############################################################################
#   Modules Revision 3.0
#   Providing a flexible user environment
#
#   File:		modules.00-init/%M%
#   Revision:		%I%
#   First Edition:	95/12/06
#   Last Mod.:		%U%, %G%
#
#   Authors:		Jens Hamisch, Jens.Hamisch@Strawberry.COM
#
#   Description:	Testuite testsequence
#   Command:
#   Sub-Command:
#
#   Comment:	%C{
#   			This module defines all patterns used further in the
#			testsuite
#		}C%
#
##############################################################################

if { $verbose > 0 } {
    send_user "\tInitializing the testsuite ...\n"
}

#
#  Regular expressions matching error and warning outputs
#

set messages "(\[^(\]+)\[(\]\[0-9\]+\[)\]"
set error_msgs "ERROR"
set warn_msgs "WARNING"
set prob_msgs "$messages:PROB:\[0-9\]+"
set verb_msgs "$messages:VERB:\[0-9\]+"
set moderr_msgs "Module $error_msgs"

# Used as a line seperator inside modules
set modlin      "\[-\]+"

# List of supported shells
set sh_shells [list sh bash ksh zsh]
set csh_shells [list csh tcsh]
set real_shells [concat $sh_shells $csh_shells [list fish]]
set other_shells [list tcl cmd perl python ruby lisp cmake r]
set supported_shells [concat $real_shells $other_shells]

# Common error responses
set err_path "$error_msgs: Unable to locate a modulefile for "
set err_file "$error_msgs: No such file or directory on "
set err_illdir "$error_msgs: Illegal operation on a directory on "
set err_nomodpath "$error_msgs: No module path defined"
set err_magic "$moderr_msgs: Magic cookie '#%Module' missing\n  In "
set err_contact "  Please contact <root@localhost>"
set err_typehelp "Try 'module --help' for more information."
set err_loinconsist "$error_msgs: Loaded environment state is inconsistent"
set err_emptymodname "$error_msgs: Invalid empty module name"
set err_emptycollname "$error_msgs: Invalid empty collection name"
set err_emptydirname "$error_msgs: Directory name empty"
set err_emptyfilename "$error_msgs: File name empty"
set err_unsatreload "$error_msgs: Cannot reload modules, some of their constraints are not satistied"
set moderr_emptycollname "$moderr_msgs: Invalid empty collection name\n  In "
set moderr_emptydirname "$moderr_msgs: Directory name empty\n  In "
set moderr_emptyfilename "$moderr_msgs: File name empty\n  In "

proc err_conflict {mod args} {
    return "$::warn_msgs: $mod cannot be loaded due to a conflict.
HINT: Might try \"module unload $args\" first."
}

proc err_conflictw {mod with} {
    return "$::warn_msgs: $mod cannot be loaded due to a conflict.
HINT: Conflict with $with is set in $mod."
}

proc err_conflictf {mod with} {
    return "$::warn_msgs: $mod conflicts with $with"
}

proc err_prereq {mod args} {
    return "$::warn_msgs: $mod cannot be loaded due to missing prereq.
HINT: the following module must be loaded first: $args"
}

proc err_prereqor {mod args} {
    return "$::warn_msgs: $mod cannot be loaded due to missing prereq.
HINT: at least one of the following modules must be loaded first: $args"
}

proc err_prerequn {mod args} {
    return "$::warn_msgs: $mod cannot be unloaded due to a prereq.
HINT: Might try \"module unload $args\" first."
}

proc err_prerequnf {mod args} {
    return "$::warn_msgs: $mod is required by [join $args {, }]"
}

proc err_prereqf {mod args} {
    return "$::warn_msgs: $mod requires [join $args { or }] loaded"
}

proc moderr_minverreq {modfile minver} {
    return "$::moderr_msgs: Modulefile requires at least Modules version $minver\n  In '$modfile'\n$::err_contact"
}

# get current terminal width
proc getTtyCols {} {
    set nbcols {}

    switch -- $::tcl_platform(os) {
        {SunOS} {
            catch {regexp {columns = (\d+);} [exec stty] match nbcols} errMsg
        }
        default {
            catch {set nbcols [lindex [exec stty size] 1]} errMsg
        }
    }
    return $nbcols
}
if {[set nbcols [getTtyCols]] ne {}} {
    set term_cols $nbcols
}
unset nbcols

# check SIP is enabled or not on OS X
if {$tcl_platform(os) eq "Darwin"} {
    catch {
        regexp {System Integrity Protection status: enabled.} [exec csrutil status] csrutil_enabled
    } errMsg
}
if {[info exists csrutil_enabled]} {
    set sip_enabled 1
    unset csrutil_enabled
} else {
    set sip_enabled 0
}

# locate siteconfig file
set siteconfig_file "$install_etcdir/siteconfig.tcl"
set siteconfig_filere [regsub -all "\(\[.+?\]\)" $siteconfig_file {\\\1}]

# determine if siteconfig forces stderr terminal attachement state
proc siteconfig_isStderrTty {} {
    interp create _siteconfig
    interp eval _siteconfig set ::siteconfig_file $::siteconfig_file
    set is_stderr_tty [interp eval _siteconfig {
        set is_stderr_tty 0
        if {[file readable $::siteconfig_file]} {
            # evaluate siteconfig file to check isStderrTty procedure
            catch {
                source $::siteconfig_file
                if {[info procs isStderrTty] eq "isStderrTty"} {
                    set is_stderr_tty [isStderrTty]
                }
            } errorMsg
        }
        return $is_stderr_tty
    }]
    interp delete _siteconfig
    return $is_stderr_tty
}

# check file permission capabilities
set test_perms_dir "$env(TESTSUITEDIR)/etc"
set test_perms_file "$test_perms_dir/modulerc"
set orig_perms [file attributes $test_perms_dir -permissions]
file attributes $test_perms_dir -permissions "ugo-rx"
set new_perms [file attributes $test_perms_dir -permissions]
if {$new_perms eq $orig_perms} {
    set is_file_perms_editable 0
} else {
    set is_file_perms_editable 1

    # check if file is readable in a locked directory
    if {[catch {
        set fid [open $test_perms_file r]
        close $fid
    }]} {
        set is_locked_dir_file_readable 0
    } else {
        set is_locked_dir_file_readable 1
    }
    file attributes $test_perms_dir -permissions $orig_perms

    # check if file is still readable if locked
    set orig_perms [file attributes $test_perms_file -permissions]
    file attributes $test_perms_file -permissions "ugo-rx"
    if {[catch {
        set fid [open $test_perms_file r]
        close $fid
    }]} {
        set is_locked_file_readable 0
    } else {
        set is_locked_file_readable 1
    }
    file attributes $test_perms_file -permissions $orig_perms
}
unset test_perms_dir
unset test_perms_file
unset orig_perms
unset new_perms

# check if symbolic link registered in git repository have been effectively
# created as symbolic link to determine if they are supported on filesystem
set is_symlink_supported [file isdirectory $env(TESTSUITEDIR)/modulefiles/symlink]

# check x11 capabilities
set x11_warn_prefix "$warn_msgs: X11 resources cannot be edited, issue spotted"
set xrdb_warn "$x11_warn_prefix\n$warn_msgs: Command 'xrdb' cannot be found"
#  check where to find the 'xrdb' binary on this system
set xrdb [auto_execok xrdb]
if {$xrdb eq ""} {
    set x11_warn $xrdb_warn
} elseif {[catch {exec $xrdb -query} errMsg]} {
    set x11_warn "$x11_warn_prefix\n$errMsg"
}

set err_fullsearch ""
# check if domainname binary is available on this system
set domainname [auto_execok domainname]
set domainname_warn "$warn_msgs: Command 'domainname' cannot be found"
if {$domainname eq ""} {
    # unavailability will add warning message to all module search queries
    set err_fullsearch $domainname_warn
}
if {$err_fullsearch ne ""} {
    set err_fullsearchnl "$err_fullsearch\n"
} else {
    set err_fullsearchnl ""
}

#
#  Set up the config-file variable if possible
#

proc default-configfile {} {
    global MODULECMD
    global config_file

    default_modulecmd
    set config_file "[file dirname $MODULECMD]/config.h"
}

default-configfile
