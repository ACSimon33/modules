if ( $?histchars ) then
   set histchar   = `echo $histchars | cut -c1`
   set _histchars = $histchars
   set prefix     = 'unset histchars;'
   set postfix    = 'set histchars = $_histchars;'
else
   set histchar = \!
   set prefix   = ''
   set postfix  = ''
endif

if ( $?prompt ) then
   set prefix  = "$prefix"'set _prompt="$prompt";set prompt="";'
   set postfix = "$postfix"'set prompt="$_prompt";unset _prompt;'
endif

if ( $?noglob ) then
   set prefix  = "$prefix"'set noglob;'
   set postfix = "$postfix"'unset noglob;'
endif

set postfix = "set _exit="'$status'"; $postfix; test 0 = "'$_exit;'

alias module $prefix'eval `@TCLSH@ @libexecdir@/modulecmd.tcl @SHELLNAME@ '$histchar'*`; '$postfix
unset _histchar
unset prefix
unset postfix

@setswitchml@# define function to switch between C and Tcl versions of Modules
@setswitchml@if ( "`alias switchml`" == '' ) then
@setswitchml@   alias switchml 'set currel=`(module --version) |& grep -E "^(Modules|VERSION=)"`; \
@setswitchml@      if ( "$currel" =~ "*Tcl*" ) set mlver="Tcl" curpath="@swmlbindir@" curmanpath="@swmlmandir@"; \
@setswitchml@      if ( "$currel" =~ "VERSION*" ) set mlver="C" curpath="@cverbindir@" curmanpath="@cvermandir@"; \
@setswitchml@      if ( $?PATH && "$curpath" != "" ) setenv PATH `echo $PATH | sed -e "s,:$curpath,," -e "s,${curpath}:,,"`; \
@setswitchml@      if ( $?MANPATH && "$curmanpath" != "" ) setenv MANPATH `echo $MANPATH | sed -e "s,:$curmanpath,," -e "s,${curmanpath}:,,"`; \
@setswitchml@      if ( "$mlver" == "Tcl" && -d @cverinitdir@ ) echo "Switching to Modules C version"; \
@setswitchml@      if ( "$mlver" == "Tcl" && -d @cverinitdir@ ) source @cverinitdir@/@SHELLNAME@; \
@setswitchml@      if ( "$mlver" == "Tcl" && ! -d @cverinitdir@ ) echo "Cannot find Modules C version"; \
@setswitchml@      if ( "$mlver" != "Tcl" ) echo "Switching to Modules Tcl version"; \
@setswitchml@      if ( "$mlver" != "Tcl" ) source @initdir@/@SHELLNAME@; \
@setswitchml@      unset currel mlver curpath curmanpath;'
@setswitchml@endif
@setswitchml@
if ( $?tcsh && $?prompt && -r @initdir@/tcsh_completion ) then
   source @initdir@/tcsh_completion
endif

# setup ENV variables to get module defined in sub-shells (works for 'sh'
# and 'ksh' in interactive mode and 'sh' (zsh-compat), 'bash' and 'ksh'
# (zsh-compat) in non-interactive mode.
setenv ENV @initdir@/profile.sh
setenv BASH_ENV @initdir@/bash

setenv MODULESHOME @prefix@

@setbinpath@if ( ":${PATH}:" !~ '*:@bindir@:*' ) then
@setbinpath@   if ( "$PATH" == '' ) then
@setbinpath@      setenv PATH @bindir@
@setbinpath@   else
@setbinpath@      setenv PATH "@bindir@:$PATH"
@setbinpath@   endif
@setbinpath@endif
@setbinpath@
@setmanpath@if ( $?MANPATH ) then
@setmanpath@   set manpath = $MANPATH
@setmanpath@else
@setmanpath@   set manpath = `manpath`
@setbinpath@endif
@setmanpath@if ( ":${manpath}:" !~ '*:@mandir@:*' ) then
@setmanpath@   if ( "$manpath" == '' ) then
@setmanpath@      setenv MANPATH @mandir@
@setmanpath@   else
@setmanpath@      setenv MANPATH "@mandir@:$manpath"
@setmanpath@   endif
@setmanpath@endif
@setmanpath@unset manpath
@setmanpath@
if (! $?MODULEPATH ) then
   if ( -r @initdir@/.modulespath ) then
      setenv MODULEPATH `sed -n 's/[ 	#].*$//; /./H; $ { x; s/^\n//; s/\n/:/g; p; }' @initdir@/.modulespath`
   else
      setenv MODULEPATH ''
   endif
endif

if (! $?LOADEDMODULES ) then
   setenv LOADEDMODULES ''
endif

# load modulerc only if module environment is empty
if ( -r @initdir@/modulerc && "$MODULEPATH" == '' && "$LOADEDMODULES" == '' ) then
   source @initdir@/modulerc
endif
