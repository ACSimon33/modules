if ($?tcsh) then
	set modules_shell="tcsh"
else
	set modules_shell="csh"
endif
if ( $?MODULE_VERSION ) then
	setenv MODULE_VERSION_STACK 	"$MODULE_VERSION"
else
	setenv MODULE_VERSION		"@VERSION@"
	setenv MODULE_VERSION_STACK	"@VERSION@"
endif
set exec_prefix='@BASEPREFIX@'

set prefix=""
set postfix=""

if ( $?histchars ) then
  set histchar = `echo $histchars | cut -c1`
  set _histchars = $histchars

  set prefix  = 'unset histchars;'
  set postfix = 'set histchars = $_histchars;'
else
  set histchar = \!
endif

if ($?prompt) then
  set prefix  = "$prefix"'set _prompt="$prompt";set prompt="";'
  set postfix = "$postfix"'set prompt="$_prompt";unset _prompt;'
endif

if ($?noglob) then
  set prefix  = "$prefix""set noglob;"
  set postfix = "$postfix""unset noglob;"
endif

alias module $prefix'eval `'$exec_prefix'/bin/modulecmd '$modules_shell' '$histchar'*`; '$postfix
unset exec_prefix
unset prefix
unset postfix


setenv MODULESHOME @prefix@

if (! $?MODULEPATH ) then
  setenv MODULEPATH `sed 's/#.*$//' ${MODULESHOME}/init/.modulespath | awk 'NF==1{printf("%s:",$1)}'`
endif

if (! $?LOADEDMODULES ) then
  setenv LOADEDMODULES ""
endif
