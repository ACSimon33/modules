if ( $?histchars ) then
   set _histchars = $histchars
   if ($?prompt) then
      alias module 'unset histchars;set _prompt="$prompt";eval `@TCLSH@ @MODULESHOME@/modulecmd.tcl @SHELLNAME@ \!*`;set histchars = $_histchars; set prompt="$_prompt";unset _prompt'
   else
      alias module 'unset histchars;eval `@TCLSH@ @MODULESHOME@/modulecmd.tcl @SHELLNAME@ \!*`;set histchars = $_histchars'
   endif
else
   if ($?prompt) then
      alias module 'set _prompt="$prompt";set prompt="";eval `@TCLSH@ @MODULESHOME@/modulecmd.tcl @SHELLNAME@ \!*`;set prompt="$_prompt";unset _prompt'
   else
      alias module 'eval `@TCLSH@ @MODULESHOME@/modulecmd.tcl @SHELLNAME@ \!*`'
   endif
endif

if ( $?tcsh && $?prompt && -r @MODULESHOME@/init/tcsh_completion ) then
   source @MODULESHOME@/init/tcsh_completion
endif

# setup ENV variables to get module defined in sub-shells (works for 'sh'
# and 'ksh' in interactive mode and 'sh' (zsh-compat), 'bash' and 'ksh'
# (zsh-compat) in non-interactive mode.
setenv ENV @MODULESHOME@/init/profile.sh
setenv BASH_ENV @MODULESHOME@/init/bash

setenv MODULESHOME @MODULESHOME@

if (! $?MODULEPATH ) then
   if ( -r @MODULESHOME@/init/.modulespath ) then
      setenv MODULEPATH `sed -n 's/[ 	#].*$//; /./H; $ { x; s/^\n//; s/\n/:/g; p; }' @MODULESHOME@/init/.modulespath`
   else
      setenv MODULEPATH ''
   endif
endif

if (! $?LOADEDMODULES ) then
   setenv LOADEDMODULES ''
endif

if ( -r @MODULESHOME@/init/modulerc ) then
   source @MODULESHOME@/init/modulerc
endif
