if ( $?histchars ) then
   set histchar   = `echo $histchars | cut -c1`
   set _histchars = $histchars
   set prefix     = 'unset histchars;'
   set postfix    = 'set histchars = $_histchars;'
else
   set histchar = \!
   set prefix   = ''
   set postfix  = ''
endif

if ( $?prompt ) then
   set prefix  = "$prefix"'set _prompt="$prompt";set prompt="";'
   set postfix = "$postfix"'set prompt="$_prompt";unset _prompt;'
endif

if ( $?noglob ) then
   set prefix  = "$prefix"'set noglob;'
   set postfix = "$postfix"'unset noglob;'
endif

set postfix = "set _exit="'$status'"; $postfix; test 0 = "'$_exit;'

alias module $prefix'eval `@TCLSH@ @MODULESHOME@/modulecmd.tcl @SHELLNAME@ '$histchar'*`; '$postfix
unset _histchar
unset prefix
unset postfix

if ( $?tcsh && $?prompt && -r @MODULESHOME@/init/tcsh_completion ) then
   source @MODULESHOME@/init/tcsh_completion
endif

# setup ENV variables to get module defined in sub-shells (works for 'sh'
# and 'ksh' in interactive mode and 'sh' (zsh-compat), 'bash' and 'ksh'
# (zsh-compat) in non-interactive mode.
setenv ENV @MODULESHOME@/init/profile.sh
setenv BASH_ENV @MODULESHOME@/init/bash

setenv MODULESHOME @MODULESHOME@

if (! $?MODULEPATH ) then
   if ( -r @MODULESHOME@/init/.modulespath ) then
      setenv MODULEPATH `sed -n 's/[ 	#].*$//; /./H; $ { x; s/^\n//; s/\n/:/g; p; }' @MODULESHOME@/init/.modulespath`
   else
      setenv MODULEPATH ''
   endif
endif

if (! $?LOADEDMODULES ) then
   setenv LOADEDMODULES ''
endif

if ( -r @MODULESHOME@/init/modulerc ) then
   source @MODULESHOME@/init/modulerc
endif
